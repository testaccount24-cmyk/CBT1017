PROC 1 NAME
CONTROL LIST
LISTDSI &NAME
SET RC = &LASTCC
IF &RC NE 0 THEN +
 WRITE I CAN'T FIND &NAME!
ELSE +
 IF &SYSDSORG NE &STR(PO) THEN +
  WRITE &NAME IS NOT PARTITIONED!
 ELSE +
  DO
   SET NOW = &SYSTIME
   SET HH = &SUBSTR(1:2,&NOW)
   SET MM = &SUBSTR(4:5,&NOW)
   SET SS = &SUBSTR(7:8,&NOW)
   SET TEMPDSN=LMXREF.T&HH&MM&SS
   LISTDSI &TEMPDSN
   SET RC = &LASTCC
   IF &RC = 0 THEN +
    DO
     CONTROL NOMSG
     DEL &TEMPDSN
     CONTROL MSG
    END
   IF &SUBSTR(1:1,&NAME) = &STR(') THEN +
    DO
     SET LEN = &LENGTH(&NAME)
     SET RPT = LMXREF.&SUBSTR(2:&LEN-1,&NAME)
    END
   ELSE +
    SET RPT = LMXREF.&NAME
   CONTROL NOMSG
   FREE FILE(SYSPRINT SYSUT1 SYSUT2)
   CONTROL MSG
   ALLOCATE FILE(SYSPRINT) DATASET(*)
   ALLOCATE FILE(SYSUT1) SHR DATASET(&NAME)
   ALLOCATE FILE(SYSUT2) SPACE(2 2) CYL DATASET(&TEMPDSN)
   CALL *(LMXREF)
   FREE FILE(SYSPRINT SYSUT1 SYSUT2)
   LISTDSI &RPT
   SET RC = &LASTCC
   IF &RC = 0 THEN +
    DO
     CONTROL NOMSG
     DEL &RPT
     CONTROL MSG
    END
   ALLOCATE FILE(SYSPRINT) DATASET(&RPT) SPACE(4 3) CYL RELEASE
   ALLOCATE FILE(SYSUT1)   DATASET(&TEMPDSN)
   CALL *(LMXREFR)
   FREE FILE(SYSPRINT SYSUT1)
   CONTROL NOMSG
   DEL &TEMPDSN
   CONTROL MSG
   ALLOCATE FILE(SYSPRINT) DATASET(*)
  END
